Name 8051_CTRL_DECODER_PAGE_CONFIG;
Assembly 0001;
Revision 1.1;
PartNo U0 G22V10;
Device G22V10;
Company SU;
Designer Atanas Bachvaroff;
Location Sofia,BG,EU;
Date 28 Aug 2019;

/* inputs / CPU */
PIN 2 = ALE;
PIN 3 = PSEN_;
PIN 4 = A15_LINE;
PIN 5 = A14_LINE;
PIN 6 = A13_LINE;
PIN 7 = RDCPU_;
PIN 8 = WRCPU_;

/* page configuration bits - 0 = default / pull-down at GAL side */
PIN 9 = PAGE1_RAM;
PIN 10 = PAGE2_RAM;
PIN 11 = PAGE3_RAM;
PIN 13 = PAGE7_RAM;

/* outputs / system / internal control bus / unbuffered / pull-up at GAL side */
PIN 23 = DIR;
PIN 22 = E_;
PIN 21 = ALE_;

/* outputs / system / external control bus / buffered / pull-up at external bus side */
PIN 20 = RD_;
PIN 19 = WR_;
PIN 18 = CSF_;
PIN 17 = CSR_;
pin 16 = CSIO_;

FIELD ADDRESS = [RDCPU_, WRCPU_, PSEN_, A15_LINE, A14_LINE, A13_LINE, PAGE1_RAM, PAGE2_RAM, PAGE3_RAM, PAGE7_RAM];
FIELD DECODE = [DIR, E_, CSIO_, CSF_, CSR_, WR_, RD_];

/* CP> 74HC574 U1 */
ALE_ = !ALE;

TABLE ADDRESS => DECODE {
/* page 0 - 8K - R/W - RAM, PSEN - flash */
	'b'011000xxxx => 'b'0011010;
	'b'101000xxxx => 'b'1011001;
	'b'110000xxxx => 'b'0010110;
	
/* page 1 - 8K - R/W/PSEN - RAM, PAGE1_CONFIG = 0, default */
	'b'0110010xxx => 'b'0011010;
	'b'1010010xxx => 'b'1011001;
	'b'1100010xxx => 'b'0011010;
/* page 1 - 8K - R/W - RAM, PSEN - flash, PAGE1_CONFIG = 1 */
	'b'0110011xxx => 'b'0011010;
	'b'1010011xxx => 'b'1011001;
	'b'1100011xxx => 'b'0010110;
	
/* page 2 - 8K - R/W/PSEN - RAM, PAGE2_CONFIG = 0, default */
	'b'011010x0xx => 'b'0011010;
	'b'101010x0xx => 'b'1011001;
	'b'110010x0xx => 'b'0011010;
/* page 2 - 8K - R/W - RAM, PSEN - flash, PAGE2_CONFIG = 1 */
	'b'011010x1xx => 'b'0011010;
	'b'101010x1xx => 'b'1011001;
	'b'110010x1xx => 'b'0010110;
	
/* page 3 - 8K - R/W/PSEN - RAM, PAGE3_CONFIG = 0, default */
	'b'011011xx0x => 'b'0011010;
	'b'101011xx0x => 'b'1011001;
	'b'110011xx0x => 'b'0011010;
/* page 3 - 8K - R/W - RAM, PSEN - flash, PAGE3_CONFIG = 1 */
	'b'011011xx1x => 'b'0011010;
	'b'101011xx1x => 'b'1011001;
	'b'110011xx1x => 'b'0010110;
	
/* page 4 - 8K - R/W/PSEN - RAM */
	'b'011100xxxx => 'b'0011010;
	'b'101100xxxx => 'b'1011001;
	'b'110100xxxx => 'b'0011010;
	
/* page 5 - 8K - R/W/PSEN - RAM */
	'b'011101xxxx => 'b'0011010;
	'b'101101xxxx => 'b'1011001;
	'b'110101xxxx => 'b'0011010;
	
/* page 6 - 8K - R/W/PSEN - RAM */
	'b'011110xxxx => 'b'0011010;
	'b'101110xxxx => 'b'1011001;
	'b'110110xxxx => 'b'0011010;
	
/* page 7 - 8K - R/W/PSEN - IO, PAGE7_CONFIG = 0, default */
	'b'011111xxx0 => 'b'0001110;
	'b'101111xxx0 => 'b'1001101;
	'b'110111xxx0 => 'b'0001110;
/* page 7 - 8K - R/W/PSEN - RAM, PAGE7_CONFIG = 1 */
	'b'011111xxx1 => 'b'0011010;
	'b'101111xxx1 => 'b'1011001;
	'b'110111xxx1 => 'b'0011010;
	
/* bus disabled / idle mode / external bus floating */
	'b'111xxxxxxx => 'b'1111111;
	
/* bus error / external bus floating */
	'b'001xxxxxxx => 'b'1111111;
	'b'010xxxxxxx => 'b'1111111;
	'b'100xxxxxxx => 'b'1111111;
	'b'000xxxxxxx => 'b'1111111;
}

