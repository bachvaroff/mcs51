Name 8051_GPO;
Assembly 0001;
Revision 1.0;
PartNo U0 G22V10;
Device G22V10;
Company DXC;
Designer Atanas Bachvaroff;
Location Sofia,BG,EU;
Date 30 May 2023;

/*
	INPUTS --
*/
PIN 14 = A0I;
PIN 13 = A1I;
PIN 11 = A2I;
PIN 10 = DEI;
PIN 9 = RSTI;
PIN 8 = WRI;
PIN 7 = CSIO77I;
PIN 6 = A7I;
PIN 5 = A12I;
PIN 4 = A11I;
PIN 3 = A10I;
PIN 2 = A9I;
PIN 1 = A8I;
/*
	-- INPUTS
*/

/*
	OUTPUTS --
*/
PIN 15 = DEO;
PIN 16 = L0LE;
PIN 17 = L1LE;
PIN 18 = L2LE;
PIN 19 = L3LE;
PIN 20 = L4LE;
PIN 21 = L5LE;
PIN 22 = OECTRL_LE;
/*
	-- OUTPUTS
*/

UPPER_HALF_IO77 = CSIO77I # !A12I;
BOARD_ID = A11I # A10I # A9I # A8I # A7I; /* 0xF000-0xF07F */
CSIO = BOARD_ID # UPPER_HALF_IO77;
DEO = RSTI # DEI # CSIO;

FIELD ADDRESS = [
	RSTI, DEO, WRI, A0I, A1I, A2I
];

FIELD DECODE = [
	L0LE, L1LE, L2LE, L3LE, L4LE, L5LE, OECTRL_LE
];

TABLE ADDRESS => DECODE {
	'b'000000 => 'b'1000000;
	'b'000001 => 'b'0100000;
	'b'000010 => 'b'0010000;
	'b'000011 => 'b'0001000;
	'b'000100 => 'b'0000100;
	'b'000101 => 'b'0000010;
	'b'000110 => 'b'0000001; /* OE ctrl */
	'b'000111 => 'b'0000001; /* OE ctrl */
	'b'001xxx => 'b'0000000;
	'b'01xxxx => 'b'0000000;
	'b'1xxxxx => 'b'0000001; /* set L*OE to 1 on reset */
}

