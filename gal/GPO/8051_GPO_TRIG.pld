Name 8051_GPO_TRIG;
Assembly 0001;
Revision 1.0;
PartNo U0 G22V10;
Device G22V10;
Company DXC;
Designer Atanas Bachvaroff;
Location Sofia,BG,EU;
Date 30 May 2023;

/*
	INPUTS --
*/
PIN 14 = A0I;
PIN 13 = A1I;
PIN 11 = A2I;
PIN 10 = DEI;
PIN 9 = RSTI;
PIN 8 = WRI;
PIN 7 = CSIO77I;
PIN 6 = A7I;
PIN 5 = A12I;
PIN 4 = A11I;
PIN 3 = A10I;
PIN 2 = A9I;
PIN 1 = A8I;
/*
	-- INPUTS
*/

/*
	OUTPUTS --
*/
PIN 15 = OELEO;
PIN 16 = L0LEO;
PIN 17 = L1LEO;
PIN 18 = L2LEO;
PIN 19 = L3LEO;
PIN 20 = L4LEO;
PIN 21 = L5LEO;
PIN 22 = DEO;
PIN 23 = LxLEO;
/*
	-- OUTPUTS
*/

UPPER_HALF_IO77 = CSIO77I # !A12I;
BOARD_ID = A11I # A10I # A9I # A8I # A7I;
CSIO = BOARD_ID # UPPER_HALF_IO77; /* 0xF000-0xF07F */
DEO = DEI # CSIO # WRI # RSTI;

FIELD ADDRESS = [
	RSTI, DEO, A2I, A1I, A0I
];

FIELD DECODE = [
	L0LEO, L1LEO, L2LEO, L3LEO, L4LEO, L5LEO, OELEO, LxLEO
];

TABLE ADDRESS => DECODE {
	'b'00000 => 'b'10000001; /* 0xF000 */
	'b'00001 => 'b'01000001; /* 0xF001 */
	'b'00010 => 'b'00100001; /* 0xF002 */
	'b'00011 => 'b'00010001; /* 0xF003 */
	'b'00100 => 'b'00001001; /* 0xF004 */
	'b'00101 => 'b'00000101; /* 0xF005 */
	'b'0011x => 'b'00000010; /* 0xF006, 0xF007 OE ctrl */
	'b'01xxx => 'b'00000000;
	'b'1xxxx => 'b'00000010; /* set L*OE to 1 on reset */
}

